apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'

def properties = new Properties()
properties.load(rootProject.file('local.properties').newDataInputStream())

android {
    signingConfigs {
        release {
            keyAlias properties['keyAlias']
            keyPassword properties['keyPassword']
            storePassword properties['storePassword']
            storeFile file('keystore.jks')
        }
        debug {
            keyAlias 'androiddebugkey'
            keyPassword 'android'
            storePassword 'android'
            storeFile file("${System.properties['user.home']}${File.separator}.android${File.separator}debug.keystore")
        }
    }

    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion

    defaultConfig {
        applicationId "com.anggrayudi.materialpreference.sample"
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode rootProject.ext.appVersionCode
        versionName "$MATERIAL_PREFERENCE_VERSION"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            zipAlignEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
            buildConfigField 'String', "BASE_64_ENCODED_PUBLIC_KEY", MATERIAL_PREFERENCE_PLAY_STORE_BASE_64_PUBLIC_KEY
        }
        debug {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.debug
            buildConfigField 'String', "BASE_64_ENCODED_PUBLIC_KEY", MATERIAL_PREFERENCE_PLAY_STORE_BASE_64_PUBLIC_KEY
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    packagingOptions {
        exclude 'META-INF/atomicfu.kotlin_module'
    }
    lintOptions {
        abortOnError false
    }
}

configurations {
    kapt
}

dependencies {
    testImplementation 'junit:junit:4.13.1'
    androidTestImplementation 'androidx.test:runner:1.3.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'

    implementation 'com.android.billingclient:billing:3.0.2'
    implementation 'androidx.recyclerview:recyclerview:1.1.0'
    implementation "org.koin:koin-android:2.1.5"

    implementation project(':materialpreference')
    compileOnly project(':annotation')
    kapt project(':processor')

//    implementation 'androidx.preference:preference:1.1.0-rc01'

//    implementation 'com.anggrayudi:materialpreference:3.6.2-SNAPSHOT'
//    implementation 'com.anggrayudi:materialpreference-api:1.1'
//    kapt 'com.anggrayudi:materialpreference-compiler:1.1'
}

tasks.withType(JavaCompile) {
    options.incremental = true
    options.annotationProcessorPath = configurations.kapt
}